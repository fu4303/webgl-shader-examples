<!DOCTYPE html>
<html lang="en">
<head>
<title>pencil shader example</title>
<meta charset="utf-8">
<meta name="viewport"
	content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
<meta name="keywords" content="shader, three.js, javaScript, examples">
<meta name="description" content="pencil shader example">
<meta name="author" content="Javier GraciÃ¡ Carpio">
<style>
body {
	background-color: #000000;
	margin: 0px;
	overflow: hidden;
}
</style>
</head>

<body>
	<div id="container"></div>

	<script id="vertexShader" type="x-shader/x-vertex">#define GLSLIFY 1
// Common varyings
varying vec3 v_position;
varying vec3 v_normal;

/*
 * The main program
 */
void main() {
    // Save the varyings
    v_position = position;
    v_normal = normalize(normalMatrix * normal);

    // Vertex shader output
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
</script>
	<script id="fragmentShader" type="x-shader/x-fragment">#define GLSLIFY 1
// Common uniforms
uniform vec2 u_resolution;
uniform vec2 u_mouse;
uniform float u_time;

// Common varyings
varying vec3 v_position;
varying vec3 v_normal;

/*
 * Returns a rotation matrix for the given angle
 */
mat2 rotate(float angle) {
    return mat2(cos(angle), -sin(angle), sin(angle), cos(angle));
}

/*
 *  Calculates the diffuse factor produced by the light illumination
 */
float diffuseFactor(vec3 normal, vec3 light_direction) {
    return -dot(normalize(normal), normalize(light_direction));
}

/*
 * Returns a value between 1 and 0 that indicates if the pixel is inside the horizontal line
 */
float horizontalLine(vec2 pixel, float y_pos, float width) {
    return 1.0 - smoothstep(-1.0, 1.0, abs(pixel.y - y_pos) - 0.5 * width);
}

/*
 * The main program
 */
void main() {
    // Use the mouse position to define the light direction
    float min_resolution = min(u_resolution.x, u_resolution.y);
    vec3 light_direction = -vec3((u_mouse - 0.5 * u_resolution) / min_resolution, 0.5);

    // Calculate the light diffusion factor
    float df = max(0.0, diffuseFactor(v_normal, light_direction));

    // Move the pixel coordinates origin to the center of the screen
    vec2 pos = gl_FragCoord.xy - 0.5 * u_resolution;

    // Rotate the coordinates by the light direction angle
    pos = rotate(atan(light_direction.y / light_direction.x) + radians(20.0)) * pos;

    // Define the first group of pencil lines
    float line_width = 7.0 * (1.0 - smoothstep(0.0, 0.3, df)) + 0.5;
    float lines_sep = 16.0;
    vec2 grid_pos = vec2(pos.x, mod(pos.y, lines_sep));
    float line_1 = horizontalLine(grid_pos, lines_sep / 2.0, line_width);
    grid_pos.y = mod(pos.y + lines_sep / 2.0, lines_sep);
    float line_2 = horizontalLine(grid_pos, lines_sep / 2.0, line_width);

    // Rotate the coordinates 50 degrees
    pos = rotate(radians(-50.0)) * pos;

    // Define the second group of pencil lines
    lines_sep = 12.0;
    grid_pos = vec2(pos.x, mod(pos.y, lines_sep));
    float line_3 = horizontalLine(grid_pos, lines_sep / 2.0, line_width);
    grid_pos.y = mod(pos.y + lines_sep / 2.0, lines_sep);
    float line_4 = horizontalLine(grid_pos, lines_sep / 2.0, line_width);

    // Calculate the sphere color
    float sphere_color = 1.0;
    sphere_color -= 0.8 * line_1 * (1.0 - smoothstep(0.5, 0.75, df));
    sphere_color -= 0.8 * line_2 * (1.0 - smoothstep(0.4, 0.5, df));
    sphere_color -= 0.8 * line_3 * (1.0 - smoothstep(0.4, 0.65, df));
    sphere_color -= 0.8 * line_4 * (1.0 - smoothstep(0.2, 0.4, df));
    sphere_color = clamp(sphere_color, 0.05, 1.0);

    // Fragment shader output
    gl_FragColor = vec4(vec3(sphere_color), 1.0);
}
</script>

	<script src="js/three.js"></script>
	<script src="js/OrbitControls.js"></script>
	<script src="js/shader-example-3d.js"></script>
</body>
</html>
